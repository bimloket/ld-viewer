PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX sh:      <http://www.w3.org/ns/shacl#>
PREFIX dash:     <http://datashapes.org/dash#>
PREFIX nen2660: <https://w3id.org/nen2660/def#>
PREFIX cbnl: <https://bimloket.nl/data/def/cbnl/> 
PREFIX cbnl-term: <https://bimloket.nl/data/term/cbnl/> 

SELECT ?entry_text ?entry_iri ?entry_icon (COUNT(?grandchild) AS ?entry_childCount)

WHERE {

#########################
# This query returns the taxonomy children to a parent term.
#
# INPUT:
#   ?term_iri: The IRI of the parent term
#########################

    {   ?child rdfs:subClassOf|rdf:type|rdfs:subPropertyOf ?term_iri .
            OPTIONAL {?grandchild rdfs:subClassOf|rdf:type|rdfs:subPropertyOf ?child . }
    } 
    UNION {
        ?term_iri rdfs:member ?child  .
            OPTIONAL {?child rdfs:member ?grandchild . }
    } 
        
     FILTER (?child != ?term_iri) .

    
    # FILTER excluding owl:Thing
    FILTER (?child != owl:Thing) .
    
    # FILTER excluding blank nodes
    FILTER (isURI(?child)) .

    # FILTER excluding Restrictions AS nodes
    FILTER NOT EXISTS {
        ?child rdf:type/rdfs:subClassOf* owl:Restriction .
    }
    
# Get child type (for icon)

    ?child rdf:type ?typeChild .
    bind ( strstarts(str(?child), str(cbnl:))||strstarts(str(?child), str(cbnl-term:)) AS ?isFromCBNL)

    BIND (?child AS ?entry_iri) .

  	BIND (IF(?isFromCBNL, "icon-term-type-w",
        IF(?typeChild = rdfs:Container, "icon-term-type-g",
            IF(?typeChild = rdfs:Class, "icon-term-type-b", 
                IF(?typeChild = owl:Class, "icon-term-type-b",
                    IF(?typeChild = rdf:Property, "icon-term-type-r", "icon-term-type-p"))))) AS ?entry_icon)

# Get child name 
    OPTIONAL {
        # Subquery om één resultaat in tree te krijgen (eerst NL, anders EN, anders localname)
        SELECT ?child ?childLabel 
        WHERE {
            OPTIONAL {?child rdfs:label | skos:prefLabel ?childLabelNL . FILTER (LANG(?childLabelNL)="nl") }
            OPTIONAL {?child rdfs:label | skos:prefLabel ?childLabelEN . FILTER(LANG(?childLabelEN)="en") }
            OPTIONAL {?child rdfs:label | skos:prefLabel ?childLabelXX . FILTER(LANG(?childLabelXX)="") }
            BIND(COALESCE(?childLabelNL, ?childLabelEN, ?childLabelXX) AS ?childLabel)
        }
    }

    BIND (IF(CONTAINS(STR(?child),"#"),STRAFTER(STR(?child), "#"),REPLACE(STR(?child),"(.*)[//]","")) AS ?childLocalname) .
    BIND (COALESCE(?childLabel, ?childLocalname) AS ?entry_text) .
}


GROUP BY ?entry_text ?entry_iri ?entry_icon
ORDER BY ?entry_text ?entry_iri ?entry_childCount